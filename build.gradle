allprojects  {
    apply plugin: 'maven'

    group = 'hr.fer.zemris.vhdllab'
    version = '2.6.7'

    defaultTasks 'clean', 'build'
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
    }

    tasks.withType(Javadoc) {
        options {
            links "http://java.sun.com/javase/6/docs/api/"
        }
    }

    repositories {
        mavenLocal()

        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://maven.springframework.org/release/" }
        maven { url "http://spring-rich-c.sourceforge.net/maven2repository" }
        maven { url "http://repository.jboss.com/maven2/" }
    }
}

description = """
    VHDLLab is a web based IDE for VHDL (programming language). It is designed
    for educational purpose as an alternative for commercial products that
    provide solutions for VHDL. The need for VHDLLab appeared at Faculty of
    Electrical Engineering and Computing, University of Zagreb, Croatia when
    VHDL was taught in Digital Electronics course. Originally WebISE was used by
    students when programming VHDL code. But WebISE has many problems (huge
    footprint, unnecessary features, inability to create automatons, lots of
    bugs, problems with license that disallows universities to distribute
    program, etc.). VHDLLab was designed as a lightweight alternative to fix
    those problems and add few other features.
""".stripIndent().trim()

def configurationPropertiesFile = project.file('configuration.properties')
if (configurationPropertiesFile.isFile()) {
    def configurationProperties = new Properties()
    configurationProperties.load(new FileReader(configurationPropertiesFile))
    configurationProperties.each { key, value ->
        project.ext.set(key, value)
    }
}
